
model{
  # Modelo 
  for(i in 1:n){
    y[i] ~ dnorm(y.hat[i], tau.y)
    y.hat[i] <- alpha + b.tipo[tipo[i]] + b.temp[temp[i]] + b.inter[tipo[i], temp[i]]
    e.y[i] <- y[i] - y.hat[i]
  }

  # Priori
  for(k in 1:3){
    b.tipo[k] ~ dnorm(0, tau.tipo)
  }
  for(j in 1:3){
    b.temp[j] ~ dnorm(0, tau.temp)
  }
  for(k in 1:3){
    for(j in 1:3){
      b.inter[k,j] ~ dnorm(0, tau.inter)
    }
  }

  # Hiperparametros
  alpha ~ dnorm(0, 0.0001)    
  tau.y <- pow(sigma.y, -2)
  sigma.y ~ dunif(0, 100)
  tau.tipo <- pow(sigma.tipo, -2)
  sigma.tipo ~ dunif(0, 100)
  tau.temp <- pow(sigma.temp, -2)
  sigma.temp ~ dunif(0, 100)
  tau.inter <- pow(sigma.inter, -2)
  sigma.inter ~ dunif(0, 100)

  # Extra
  sd.tipo <- sd(b.tipo[])
  sd.temp <- sd(b.temp[])
  sd.inter <- sd(b.inter[,])
  sd.error <- sd(e.y[])
} 